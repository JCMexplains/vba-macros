Option Explicit

'@EntryPoint
Public Sub Format_book_orders()
    '
    ' Format_book_orders Macro
    
    Dim folderPath  As String
    folderPath = Application.ActiveWorkbook.Path
    'path without the file name; does not include the final forward slash
    'There is a difference between ActiveWorkbook and ThisWorkbook--Google it
    
    Dim newName     As String
    newName = folderPath & "/" & "Book orders " & MonthName(Month(Date)) & " " & Day(Date) & ".xlsx"

    ActiveWorkbook.SaveAs Filename:=newName, FileFormat:=xlOpenXMLWorkbook
    'including FileFormat ensures this will save as xlsx even if it was originally csv
    
    Dim lastRow     As Long
    Dim lastCol     As Long

    lastRow = Cells(Rows.Count, 1).End(xlUp).Row
    'gets the last used row
    
    lastCol = Cells(8, Columns.Count).End(xlToLeft).Column
    'gets the last used column. row 8 is the first fully-filled row
    
    Dim orderData As Range
    Set orderData = ActiveSheet.Range(Cells(8, 1), Cells(lastRow, lastCol))

    Rows("1:7").Select
    Selection.Delete Shift:=xlToLeft
    Columns("A:B").Select
    Selection.Delete Shift:=xlToLeft
    Application.CutCopyMode = False

    ActiveSheet.ListObjects.Add(SourceType:=xlSrcRange, Source:=orderData, xlListObjectHasHeaders:=xlYes, tablestyleName:="TableStyleMedium2").Name = "orderDataTable"

    SelectColumnFromTable "ISBN"
    Selection.NumberFormat = "0"
    
    SelectColumnFromTable "SKU"
    Selection.Delete Shift:=xlToLeft
    
    Columns("A:Z").EntireColumn.AutoFit
    
    ActiveSheet.ListObjects("orderDataTable").Range.AutoFilter Field:=1, Criteria1:= _
                                                               Array("AML", "ENC", "ENG", "LIT"), Operator:=xlFilterValues
    ActiveSheet.ListObjects( _
        "orderDataTable").Sort.SortFields.Clear
    ActiveSheet.ListObjects( _
        "orderDataTable").Sort.SortFields.Add2 Key:=Range("orderDataTable[[#All],[Instructor]]"), _
        SortOn:=xlSortOnValues, Order:=xlAscending, DataOption:= _
        xlSortTextAsNumbers
    With ActiveSheet.ListObjects( _
         "orderDataTable").Sort
        .Header = xlYes
        .MatchCase = False
        .Orientation = xlTopToBottom
        .Apply
    End With
End Sub

Public Sub SelectColumnFromTable(ByVal columnHeader As String)
    
    'we will use these cells to specify a column in the table
    Dim topCell As Range
    Dim bottomCell As Range
    Dim structuredRefString As String
    
    structuredRefString = "orderDataTable[[#Headers],[" & columnHeader & "]]"
    
    Set topCell = Range(structuredRefString)
    Set bottomCell = Range(structuredRefString).End(xlDown)
    
    Range(topCell, bottomCell).Select
    
End Sub





